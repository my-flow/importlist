import java.nio.charset.StandardCharsets
import java.util.jar.Attributes

import java.text.SimpleDateFormat

import org.apache.commons.configuration2.FileBasedConfiguration
import org.apache.commons.configuration2.PropertiesConfiguration

import proguard.gradle.ProGuardTask


apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile(':moneydance:')
    compile('org.apache.commons:commons-lang3:3.7')
    compile('org.apache.commons:commons-configuration2:2.3')
    compile('org.apache.commons:commons-text:1.4')
    compile('commons-io:commons-io:2.6')
    compile('org.slf4j:slf4j-api:1.7.25')
    compile('org.slf4j:slf4j-nop:1.7.25')
    compile('org.checkerframework:checker:2.3.2')

    findbugsPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:7.4.3'
}

jar {
    baseName rootProject.name
    manifest {
        attributes ((Attributes.Name.IMPLEMENTATION_TITLE.toString()): getConfigValueForKey("module_name"),
                    (Attributes.Name.IMPLEMENTATION_VERSION.toString()): getConfigValueForKey("module_build"),
                    (Attributes.Name.IMPLEMENTATION_VENDOR.toString()): getConfigValueForKey("vendor"),
                    'Built-By': System.getProperty('user.name'),
                    'Built-Date': getCurrentTimestamp(),
                    'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')')

    }
}

assemble.dependsOn shadowJar
build.dependsOn shadowJar

shadowJar {
    baseName rootProject.name
    from 'NOTICE'
    from 'LICENSE'
    exclude 'META-INF/**/*'

    dependencies {
        include dependency('org.apache.commons:commons-configuration2:2.3')
        include dependency('org.apache.commons:commons-lang3:3.7')
        include dependency('org.apache.commons:commons-text:1.4')
        include dependency('commons-io:commons-io:2.6')
        include dependency('org.slf4j:slf4j-api:1.7.25')
        include dependency('org.slf4j:slf4j-nop:1.7.25')
    }
}

task proguard(type: ProGuardTask, dependsOn: 'shadowJar') {
    injars  shadowJar.archivePath
    outjars "${project.distsDir}/proguard-${rootProject.name}.jar"
    configuration 'config/proguard.cfg'
}

proguard.doLast {
    new File("${project.distsDir}/proguard-${rootProject.name}.jar").renameTo mxtfile as String
}

shadowJar.finalizedBy proguard

javadoc {
    options.author = true
    options.version = true
    options.memberLevel = JavadocMemberLevel.PACKAGE
    options.use = true
    options.linkSource = true
    options.docTitle = 'Import List for Moneydance'
    options.windowTitle = 'Import List for Moneydance'
    options.links = [
        'https://docs.oracle.com/javase/8/docs/api/',
        'http://moneydance.com/dev/apidoc/',
        'http://www.slf4j.org/apidocs/',
        'https://commons.apache.org/proper/commons-configuration/apidocs/',
        'https://commons.apache.org/proper/commons-io/javadocs/api-2.6/',
        'https://commons.apache.org/proper/commons-lang/javadocs/api-3.7/',
        'https://commons.apache.org/proper/commons-text/javadocs/api-1.4/',
        'https://commons.apache.org/proper/commons-logging/javadocs/api-release/'
    ]
    options.footer = '<a href="https://github.com/my-flow/importlist" target="_blank">Import List on GitHub</a>'
}

String getCurrentTimestamp() {
  Date today = new Date()
  SimpleDateFormat df = new SimpleDateFormat('yyyy-MM-dd HH:mm:ss')
  return df.format(today)
}

String getConfigValueForKey(String key) {
    file('src/main/resources/com/moneydance/modules/features/importlist/meta_info.dict').withInputStream {
        final FileBasedConfiguration config = new PropertiesConfiguration();
        config.read(new InputStreamReader(it, StandardCharsets.UTF_8))
        return config.getString("\"" + key + "\"").replaceAll("^\"|\"\$", "");
    }
}