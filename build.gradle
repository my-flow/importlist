buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath 'org.apache.commons:commons-configuration2:2.3'
        classpath 'net.sf.proguard:proguard-gradle:6.0.2'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'findbugs'

    sourceCompatibility = 1.8

    ext {
        privkeyid = '99'
        privkeyfile = "$rootDir/config/priv_key"
        pubkeyfile = "$rootDir/config/pub_key"
        mxtfile = "${project(':core').distsDir}/${rootProject.name}.mxt"
    }

    repositories {
        mavenCentral()
        flatDir dirs: "$rootDir/lib"
    }

    findbugs {
        effort = "max"
        reportLevel = "medium"
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    task checkstyleHtml(dependsOn: checkstyleMain) {
        if (checkstyleMain.reports.xml.destination.exists()) {
            ant.xslt(in: checkstyleMain.reports.xml.destination,
                    style: file("$rootDir/config/checkstyle/checkstyle-noframes-sorted.xsl"),
                    out: new File(checkstyleMain.reports.xml.destination.parent as String, 'main.html'))
        }
    }

    checkstyleMain.finalizedBy checkstyleHtml

    pmd {
        sourceSets = [sourceSets.main]
        ruleSetFiles = files('config/ruleSet.xml')
    }
}
